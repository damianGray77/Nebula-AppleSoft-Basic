0000 GOTO 1000
0010 FOR F = 0 TO FS
0020    CALL 62450
0030    HPLOT SX(F, 0), SY(F, 0) TO SX(F, 1), SY(F, 1)
0040    HPLOT SX(F, 1), SY(F, 1) TO SX(F, 2), SY(F, 2)
0050    HPLOT SX(F, 2), SY(F, 2) TO SX(F, 3), SY(F, 3)
0060    HPLOT SX(F, 3), SY(F, 3) TO SX(F, 0), SY(F, 0)
0070    HPLOT SX(F, 4), SY(F, 4) TO SX(F, 5), SY(F, 5)
0080    HPLOT SX(F, 5), SY(F, 5) TO SX(F, 6), SY(F, 6)
0090    HPLOT SX(F, 6), SY(F, 6) TO SX(F, 7), SY(F, 7)
0100    HPLOT SX(F, 7), SY(F, 7) TO SX(F, 4), SY(F, 4)
0110    HPLOT SX(F, 0), SY(F, 0) TO SX(F, 4), SY(F, 4)
0120    HPLOT SX(F, 1), SY(F, 1) TO SX(F, 5), SY(F, 5)
0130    HPLOT SX(F, 2), SY(F, 2) TO SX(F, 6), SY(F, 6)
0140    HPLOT SX(F, 3), SY(F, 3) TO SX(F, 7), SY(F, 7)
0150    SC = NOT SC
0160    POKE 49236 + NOT SC, 0
0170    POKE 230, 32 + 32 * SC
0180 NEXT F
0190 GOTO 10

1000 SZ = 50 : CX = 140 : CY = 96
1010 SC = 0
1020 A = 0 : F = 0 : FS = 719 : AD = 6.2832 / (FS + 1)
1030 DIM PX(7) : DIM PY(7) : DIM PZ(7)
1040 PX(0) = -1: PY(0) = -1 : PZ(0) = -1
1050 PX(1) =  1: PY(1) = -1 : PZ(1) = -1
1060 PX(2) =  1: PY(2) =  1 : PZ(2) = -1
1070 PX(3) = -1: PY(3) =  1 : PZ(3) = -1
1080 PX(4) = -1: PY(4) = -1 : PZ(4) =  1
1090 PX(5) =  1: PY(5) = -1 : PZ(5) =  1
1100 PX(6) =  1: PY(6) =  1 : PZ(6) =  1
1110 PX(7) = -1: PY(7) =  1 : PZ(7) =  1
1120 DIM SX(FS, 7) : DIM SY(FS, 7)
1130 FOR I = 0 TO FS
1140    C = COS(A) : S = SIN(A)
1150    FOR N = 0 TO 7
1160       SX(I, N) = (C * (-C * PX(N) + S * PY(N)) + S * PZ(N)) * SZ + CX
1170       SY(I, N) = (S * PX(N) + C * PY(N)) * SZ + CY
1180    NEXT N
1190    A = A + AD
1200 NEXT I
1210 A = 0
1220 HGR : HGR2 
1230 HCOLOR = 3
1240 GOTO 10
